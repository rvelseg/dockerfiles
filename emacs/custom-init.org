#+TITLE: emacs init file
#+PROPERTY: header-args    :tangle yes :comments link
#+INFOJS_OPT: view:content toc:nil

* First things first
** Encoding

 Use UTF-8 es the default, [[https://www.masteringemacs.org/article/working-coding-systems-unicode-emacs][ref]].
 #+begin_src emacs-lisp
 (prefer-coding-system 'utf-8)
 (set-default-coding-systems 'utf-8)
 (set-terminal-coding-system 'utf-8)
 (set-keyboard-coding-system 'utf-8)
 ;; backwards compatibility as default-buffer-file-coding-system
 ;; is deprecated in 23.2.
 (if (boundp 'buffer-file-coding-system)
     (setq-default buffer-file-coding-system 'utf-8)
   (setq default-buffer-file-coding-system 'utf-8))
 ;; Treat clipboard input as UTF-8 string first; compound text next, etc.
 (setq x-select-request-type '(UTF8_STRING COMPOUND_TEXT TEXT STRING))
 #+end_src

** Packages

First, add to =load-path= the paths to packages outside the repos.
#+begin_src emacs-lisp
  (add-to-list 'load-path "~/.emacs.d/not_from_pm/")
#+end_src
But from [[https://www.emacswiki.org/emacs/LoadPath][emacsWiki]]
#+BEGIN_QUOTE
Adding a directory to variable ‘load-path’ does not also add any of its descendants
#+END_QUOTE
then, to include subdirectories
#+begin_src emacs-lisp
  (let ((default-directory "~/.emacs.d/not_from_pm/"))
    (normal-top-level-add-subdirs-to-load-path))
#+end_src

Add the sandbox to =load-path=, as well
#+begin_src emacs-lisp
  (add-to-list 'load-path "~/.emacs.d/sandbox/")
  (let ((default-directory "~/.emacs.d/sandbox/"))
    (normal-top-level-add-subdirs-to-load-path))
#+end_src

Define repos
#+begin_src emacs-lisp
  (require 'package)
  (setq package-archives '(("gnu" . "http://elpa.gnu.org/packages/")
			   ("melpa" . "http://melpa.milkbox.net/packages/")
			   ("org" . "http://orgmode.org/elpa/")
			   ;;("marmalade" . "https://marmalade-repo.org/packages/")
			   ))
#+end_src

And, initialize. This will also change the value of =load-path=, then it is good to have it at the beginning of the init file.
#+begin_src emacs-lisp
;;   (package-initialize)
#+end_src
I disabled this line because of the automatically added =(package-initialize)=, by emacs 25, at the begining of my =init.el=. It seems to be really important to run this at the very beginning of the init file.

QnD patch, the wrong version of org was been loaded
#+begin_src emacs-lisp
  ;; (org-reload)
  ;; (message (org-version))
#+end_src

** =customize= system

 Use a separate file for the code automatically generated by =customize=
 #+begin_src emacs-lisp
   (setq custom-file (expand-file-name "customize.el" user-emacs-directory))
   (load custom-file)
 #+end_src

** Server

Start an emacs server if not already started
#+begin_src emacs-lisp
  (require 'server)
  (or (server-running-p)
      (server-start))
#+end_src

* Global behavior

No explanations in scratch buffer, [[http://stackoverflow.com/questions/1498258][ref]].
#+begin_src emacs-lisp
  (setq initial-scratch-message ";; Scratch buffer")
#+end_src

No blinking
#+begin_src emacs-lisp
  (blink-cursor-mode 0)
#+end_src

No toolbar
#+begin_src emacs-lisp
  (tool-bar-mode -1)
#+end_src

No splash screen
#+begin_src emacs-lisp
  (setq inhibit-splash-screen t)
#+end_src

No menu bar
#+begin_src emacs-lisp
  (menu-bar-mode -1)
#+end_src

No scroll bars
#+begin_src emacs-lisp
  (scroll-bar-mode -1)
#+end_src

Short answers y/n
#+begin_src emacs-lisp
  (fset 'yes-or-no-p 'y-or-n-p)
#+end_src

Numbers in lines and columns
#+begin_src emacs-lisp
  (setq column-number-mode t)
  (setq line-number-mode t)
#+end_src

Syntax highlighting
#+begin_src emacs-lisp
  (cond ((fboundp 'global-font-lock-mode)
         (global-font-lock-mode t)
         (setq font-lock-maximum-decoration t)))
#+end_src

Region highlighting
#+begin_src emacs-lisp
  (setq-default transient-mark-mode t)
#+end_src

Always end a file with a newline
#+begin_src emacs-lisp
  (setq require-final-newline t)
#+end_src

Stop at the end of the file, don't add new lines
#+begin_src emacs-lisp
  (setq next-line-add-newlines nil)
#+end_src

killing and yanking, friendly with external applications
#+begin_src emacs-lisp
  (global-set-key "\C-w" 'clipboard-kill-region)
  (global-set-key "\M-w" 'clipboard-kill-ring-save)
  (global-set-key "\C-y" 'clipboard-yank)
  ;; Cycling backwards through the kill-ring
  ;; http://emacswiki.org/emacs/KillingAndYanking
  (defun yank-pop-forwards (arg)
        (interactive "p")
        (yank-pop (- arg)))
  (global-set-key "\M-Y" 'yank-pop-forwards)
  ;; delete selection mode
  (delete-selection-mode 1)
#+end_src

Word completion with C-RET and M-RET, based in the previous words you typed
#+begin_src emacs-lisp
  (dynamic-completion-mode)
#+end_src

Enable some commands, disabled by default because they could be confusing
#+begin_src emacs-lisp
  (put 'upcase-region 'disabled nil)
  (put 'scroll-left 'disabled nil)
#+end_src

Visible bell
#+begin_src emacs-lisp
  (setq visible-bell t)
#+end_src

Highlight parens
#+begin_src emacs-lisp
  (show-paren-mode 1)
#+end_src

Scroll buffer line by line
#+begin_src emacs-lisp
  ;; (fset 'custom-down-one-line
  ;;    "\C-u1\C-v")
  ;; (global-set-key (kbd "M-<down>") 'custom-down-one-line)
  ;; (fset 'custom-up-one-line
  ;;    "\C-u1\M-v")
  ;; (global-set-key (kbd "M-<up>") 'custom-up-one-line)
#+end_src
issues:
- collision with org mode move subtree, using smoth-scrolling instead.

Enable some commands disabled by default
#+begin_src emacs-lisp
  (put 'downcase-region 'disabled nil)
#+end_src
I acctually move here the code that emacs automatically writes in my =init.el= when I answer yes to the question to enable such commands.

** Unfill paragraph

Stefan Monnier <foo at acm.org>. It is the opposite of fill-paragraph.
#+begin_src emacs-lisp
  (defun unfill-paragraph ()
    "Takes a multi-line paragraph and makes it into a single line of text."
    (interactive)
    (let ((fill-column (point-max)))
      (fill-paragraph nil)))
  ;; Handy key definition
  (define-key global-map "\M-Q" 'unfill-paragraph)
#+end_src

* Shells

** Shell

 #+begin_src emacs-lisp
 (defun clear-shell ()
    (interactive)
    (let ((old-max comint-buffer-maximum-size))
      (setq comint-buffer-maximum-size 0)
      (comint-truncate-buffer)
      (setq comint-buffer-maximum-size old-max)))
 #+end_src

** Term

Thanks to [this post](https://emacs.stackexchange.com/questions/5585)
#+begin_src emacs-lisp
  (require 'term)

  (defun jnm/term-toggle-mode ()
    "Toggles term between line mode and char mode"
    (interactive)
    (if (term-in-line-mode)
	(term-char-mode)
      (term-line-mode)))

  (define-key term-mode-map (kbd "C-c C-j") 'jnm/term-toggle-mode)
  (define-key term-mode-map (kbd "C-c C-k") 'jnm/term-toggle-mode)

  (define-key term-raw-map (kbd "C-c C-j") 'jnm/term-toggle-mode)
  (define-key term-raw-map (kbd "C-c C-k") 'jnm/term-toggle-mode)
#+end_src

* Gnowsys

One of these days I'm going to learn how to use this

Gnowsys mode?

* Images

http://vwood.github.io/emacs-images-in-buffer.html
#+begin_src emacs-lisp
(defun refresh-iimages ()
  "Only way I've found to refresh iimages (without also recentering)"
  (interactive)
  (clear-image-cache nil)
  (iimage-mode nil)
  (iimage-mode t))
;;
(add-to-list 'compilation-finish-functions
             (lambda (buffer msg)
	       (save-excursion
                 (set-buffer buffer)
                 (refresh-iimages))))
#+end_src

* pdf-tools

#+begin_src emacs-lisp
  (pdf-tools-install)
  (add-hook 'pdf-view-mode-hook 'auto-revert-mode)
#+end_src
TODO: Update custom key binding in the way docview had it.

* CSV files

http://stackoverflow.com/questions/10616525
#+begin_src emacs-lisp
  (setq csv-separators '("," ";" "|" " "))
#+end_src

* Magit

In case is wanted to load the github based instance
#+begin_src emacs-lisp
  ;; (add-to-list 'load-path "~/.emacs.d/site-lisp/magit")
#+end_src

Key binding
#+begin_src emacs-lisp
  (global-set-key (kbd "S-<f3>") 'magit-status)
  (global-set-key (kbd "C-x v d") 'magit-status)
  (global-set-key (kbd "C-x v f") 'magit-find-file)
#+end_src

Highlight white space
#+begin_src emacs-lisp
  (setq smerge-refine-ignore-whitespace nil)
#+end_src

Highlight differences in hunks
#+begin_src emacs-lisp
  (setq magit-diff-refine-hunk 'all)
#+end_src

* Hyde

#+begin_src emacs-lisp
  (require 'hyde)
#+end_src

* Smartparens

#+begin_src emacs-lisp
  (require 'smartparens-config)
#+end_src

* Auto upgrade

It seems like a good idea, but it has some issues:
- since packages are updated when emacs starts, and emacs should be restarted after package upgrade; then, I often have to start emacs twice, or more. If I forget, sometimes emacs have strange behaviors.
- Org installation depends on the status of emacs. The documentation says "Important: you need to do this [install] in a session where no .org file has been visited, i.e., where no Org built-in function have been loaded. Otherwise autoload Org functions will mess up the installation." Does other packages have this kind of problems?
Then, this is disabled.
#+begin_src emacs-lisp
  ;; (spu-package-upgrade-daily)
#+end_src

* Gitlab integration

#+begin_src emacs-lisp
  (require 'gitlab)
#+end_src

* History

** Minibuffer

 #+begin_src emacs-lisp
   (setq savehist-file "~/.emacs.d/history/minibufer")
   (savehist-mode 1)
   (setq history-length t)
   (setq history-delete-duplicates t)
   (setq savehist-save-minibuffer-history 1)
   (setq savehist-additional-variables
         '(kill-ring search-ring
                     regexp-search-ring))
 #+end_src

** Tramp

 #+begin_src emacs-lisp
   (setq tramp-persistency-file-name "~/.emacs.d/history/tramp")
 #+end_src

** Recent files

If you are going change the =recentf-save-file=, do it before enabling the mode, [[https://www.reddit.com/r/emacs/comments/2bxmc5/recentf_overwriting_recentflist/][ref]].
There is a problem with tramp, it could slow down the startup, [[http://stackoverflow.com/questions/880625/stop-tramp-mode-running-on-emacs-startup][ref]].
 #+begin_src emacs-lisp
   (require 'recentf)
   (setq recentf-save-file "~/.emacs.d/history/recentf")   ;; custom list file location
   (setq recentf-auto-cleanup 'never)                      ;; patch for tramp at startup
;;   (run-at-time nil (* 5 60) 'recentf-save-list)           ;; save recent file list regularly
   (recentf-mode 1)                                        ;; activate
 #+end_src

** Backups

 From Sacha Chua
 #+begin_src emacs-lisp
  (setq backup-directory-alist '(("." . "~/.emacs.d/history/backups")))
  (setq delete-old-versions -1)      ;; Don't
  (setq version-control t)           ;; Number unlimited backups
  (setq vc-make-backup-files t)      ;; Do backups for VC registered files
  (setq auto-save-file-name-transforms '((".*" "~/.emacs.d/history/auto-save-list/" t)))
  (setq auto-save-list-file-prefix "~/.emacs.d/history/auto-save-list/.saves-")
 #+end_src

** Persistent command history in comint shells

https://oleksandrmanzyuk.wordpress.com/2011/10/23/a-persistent-command-history-in-emacs/
#+begin_src emacs-lisp
  (defun comint-write-history-on-exit (process event)
    (comint-write-input-ring)
    (let ((buf (process-buffer process)))
      (when (buffer-live-p buf)
	(with-current-buffer buf
          (insert (format "\nProcess %s %s" process event))))))
  ;;
  (defun turn-on-comint-history ()
    (let ((process (get-buffer-process (current-buffer))))
      (when process
	(setq comint-input-ring-file-name
              (format "~/.emacs.d/history/%s"
		      (replace-regexp-in-string "/" "%" (process-name process))
                      ))
	(comint-read-input-ring)
	(set-process-sentinel process
                              #'comint-write-history-on-exit))))
  ;;
  (add-hook 'inferior-python-mode-hook 'turn-on-comint-history)
  (add-hook 'gnuplot-comint-mode-hook 'turn-on-comint-history)
  (add-hook 'kill-buffer-hook 'comint-write-input-ring)
  ;;
  (defun mapc-buffers (fn)
    (mapc (lambda (buffer)
            (with-current-buffer buffer
              (funcall fn)))
          (buffer-list)))
  ;;
  (defun comint-write-input-ring-all-buffers ()
    (mapc-buffers 'comint-write-input-ring))
  ;;
  (add-hook 'kill-emacs-hook 'comint-write-input-ring-all-buffers)
#+end_src

** Desktop mode

By now, disabled. I find myself too often using =emacs --no-desktop= to have a fresh state depending only on the init file, and be able to debug it.
 #+begin_src emacs-lisp
   (desktop-save-mode -1)
   ;; there is a problem restoring tex files when using the whitespace mode
   (setq desktop-buffers-not-to-save
           (concat "\\("
                   "^nn\\.a[0-9]+\\|\\.log\\|(ftp)\\|^tags\\|^TAGS"
                   "\\|\\.emacs.*\\|\\.diary\\|\\.newsrc-dribble\\|\\.bbdb|\\.tex"
                   "\\)$"))
 #+end_src

* Navigation

** helm

 Load
 #+begin_src emacs-lisp
   (require 'helm-config)
 #+end_src

 The default =C-x c= is quite close to =C-x C-c=, which quits Emacs.
 Changed to =C-c h=. Note: We must set =C-c h= globally, because we cannot
 change =helm-command-prefix-key= once =helm-config= is loaded.
 #+begin_src emacs-lisp
   (global-set-key (kbd "C-c h") 'helm-command-prefix)
   (global-unset-key (kbd "C-x c"))
 #+end_src

 Map
 #+begin_src emacs-lisp
  (with-eval-after-load 'helm
    (define-key helm-map (kbd "C-c p") 'ignore)
    (define-key helm-map (kbd "<tab>") 'helm-select-action)
    (define-key helm-map (kbd "C-i")   'helm-execute-persistent-action)  ; make TAB works in terminal
    (define-key helm-map (kbd "C-z")   'helm-select-action)              ; list actions using C-z
    (define-key helm-map (kbd "C-f")   'my-make-helm-full-frame)         ; Use the full frame
    )
 #+end_src

 Use =curl= if present
 #+begin_src emacs-lisp
   (when (executable-find "curl")
     (setq helm-google-suggest-use-curl-p t))
 #+end_src

 Use the full frame
 http://emacs.stackexchange.com/questions/643
 #+begin_src emacs-lisp
  (defun my-make-helm-full-frame ()
    (interactive)
    (with-selected-window (helm-window)
      (delete-other-windows)))
 #+end_src

 Options
 #+begin_src emacs-lisp
  (setq helm-split-window-in-side-p           t ; open helm buffer inside current window, not occupy whole other window
	helm-move-to-line-cycle-in-source     t ; move to end or beginning of source when reaching top or bottom of source.
	helm-ff-search-library-in-sexp        t ; search for library in `require' and `declare-function' sexp.
	helm-scroll-amount                    8 ; scroll 8 lines other window using M-<next>/M-<prior>
	helm-ff-file-name-history-use-recentf t
	helm-split-window-default-side        'left    ; Use the left side of the window
	)
 #+end_src

 Key bindings
 #+begin_src emacs-lisp
  (global-set-key (kbd "M-x") 'helm-M-x)
  (global-set-key (kbd "C-x r b") 'helm-filtered-bookmarks)
  (global-set-key (kbd "C-x b") 'helm-buffers-list)
  (global-set-key (kbd "C-x C-b") 'helm-buffers-list)
  (global-set-key (kbd "C-x C-f") 'helm-find-files)
  (global-set-key (kbd "M-y") 'helm-show-kill-ring)
  (global-set-key (kbd "C-c h h") 'helm-mini)
  (global-set-key (kbd "C-x C-r") 'helm-for-files)
 #+end_src

 Use external programs to open files
 #+begin_src emacs-lisp
   (setq helm-external-programs-associations
         '(("pdf" . "evince")
           ("png" . "eog")
           ("jpg" . "eog")))
 #+end_src

 Activate
 #+begin_src emacs-lisp
   (helm-mode 1)
 #+end_src

*** Descbindings

 #+begin_src emacs-lisp
   (require 'helm-descbinds)
   (helm-descbinds-mode)
   ;; (global-set-key (kbd "C-h b") 'helm-descbinds)
 #+end_src

*** Bibtex

 Load
 #+begin_src emacs-lisp
   (autoload 'helm-bibtex "helm-bibtex" "" t)
 #+end_src

 External pdf viewer
 #+begin_src emacs-lisp
   (setq bibtex-completion-pdf-open-function
     (lambda (fpath)
       (call-process "okular" nil 0 nil fpath)))
 #+end_src

 pdf file field
 #+begin_src emacs-lisp
   (setq bibtex-completion-pdf-field "File")
 #+end_src

 Custom function ot use local files
#+begin_src emacs-lisp
  (defun helm-bibtex-with-dir-local-bibliography (&optional arg)
    "Search BibTeX entries with directory local bibliography file.

  With a prefix ARG the cache is invalidated and the bibliography
  reread. WARNING: if you set variables with 'dir-local.el' they
  could break this function, IDKW."
    (interactive "P")
    (let ((bibtex-completion-bibliography (list "./bibtex/ref.bib"))
	  (bibtex-completion-library-path (list "./references/"))
	  (bibtex-completion-notes-path "./notes/")
	  (bibtex-completion-notes-extension ".md"))
      (helm-bibtex arg)))
#+end_src

 Key bindings
 #+begin_src emacs-lisp
   (global-set-key (kbd "C-c h b") nil)
   (global-set-key (kbd "C-c h b b") 'helm-bibtex)
   (global-set-key (kbd "C-c h b l") 'helm-bibtex-with-local-bibliography)
   (global-set-key (kbd "C-c h b d") 'helm-bibtex-with-dir-local-bibliography)
 #+end_src

** Projectile

 #+begin_src emacs-lisp
   (require 'projectile)
   (projectile-global-mode)
   (setq projectile-file-exists-remote-cache-expire nil)
   ;; (setq projectile-switch-project-action 'projectile-dired)
   ;; (persp-mode)
   ;; (require 'persp-projectile)
   ;; (define-key projectile-mode-map (kbd "s-s") 'projectile-persp-switch-project)
   (require 'helm-projectile)
   (helm-projectile-on)
 #+end_src

The following doesn't work. I don't know why, I loaded the key bindings with =helm-projectile-on= above.
#+begin_src emacs-lisp
  ;; (global-set-key (kbd "C-c p f") 'helm-projectile-find-file)
#+end_src

Switch between org and pdf files
#+begin_src emacs-lisp
  (add-to-list 'projectile-other-file-alist '("org" "pdf"))
  (add-to-list 'projectile-other-file-alist '("pdf" "org"))
#+end_src

Don't bookmark remote projects, from [[https://github.com/martinp/emacs.d][martinp]]
#+begin_src emacs-lisp
  (setq projectile-ignored-project-function 'file-remote-p)
#+end_src

** Neotree

 Key binding
 #+begin_src emacs-lisp
   (global-set-key (kbd "S-<f1>") 'neotree-toggle)
 #+end_src

** Dired

 Load extensions
 #+begin_src emacs-lisp
   (require 'dired-x)
   (require 'dired-k)
   (require 'dired+)
 #+end_src

 Human readable filesizes
 #+begin_src emacs-lisp
 (setq dired-listing-switches "-alh")
 #+end_src

 Hide hidden files, and details
 #+begin_src emacs-lisp
   (setq dired-omit-files "^\\...+$")
   (add-hook 'dired-mode-hook (lambda () (dired-omit-mode 1)))
   (add-hook 'dired-mode-hook 'dired-hide-details-mode)
 #+end_src

 Visit multiple files
 http://stackoverflow.com/questions/1110118/
 #+begin_src emacs-lisp
 (eval-after-load "dired"
   '(progn
      (define-key dired-mode-map "F" 'my-dired-find-file)
      (defun my-dired-find-file (&optional arg)
	"Open each of the marked files, or the file under the point, or when prefix arg, the next N files "
	(interactive "P")
	(let* ((fn-list (dired-get-marked-files nil arg)))
          (mapc 'find-file fn-list)))))
 #+end_src

 Default shell commands, [[https://emacs.stackexchange.com/questions/3698][ref]]. This depends on =dired-x=. See =openwith= as well.
 #+begin_src emacs-lisp
   (setq dired-guess-shell-alist-user
         '(("\\.e?ps$" "okular" "evince")
           ("\\.chm$" "xchm")
           ("\\.rtf?$" "libreoffice")
           ("\\.docx?$" "libreoffice")
           ("\\.xlsx?$" "libreoffice")
           ("\\.pptx?$" "libreoffice")
           ("\\.od[stp]$" "libreoffice")
           ("\\.mp4$" "mplayer" "totem")
           ("\\.flv$" "mplayer" "totem")
           ("\\.mov$" "mplayer" "totem")
           ("\\.3gp$" "mplayer" "totem")
           ("\\.avi$" "mplayer" "totem")
           ("\\.png$" "eog" "gimp")
           ("\\.jpg$" "eog" "gimp")
           ("\\.JPG$" "eog" "gimp")
           ("\\.rar$" "unrar x")
           ("\\.pdf$" "okular" "evince")
           ("\\.djvu?$" "okular" "evince")
           ("\\.nii\\.gz$" "fslview")))
 #+end_src
Issues:
- When in a remote server, using TRAMP
  - this commands doesn't behave in a DWIM way, the process is executed in the remote machine, and fails because of the lack of an X server through TRAMP. What I would like is that the command
  - if a process has been started with =!= or with =&=, even on a local file, the execution of other precesses (on remote systems) is locked, emacs gives you the option to kill the other processs, but not to create a new buffer with a new process.

Another approach [[http://stackoverflow.com/questions/13901955/how-to-avoid-pop-up-of-async-shell-command-buffer-in-emacs][taken from here]].
#+begin_src emacs-lisp
;list of programs, corresponding to extensions
(setq alist-programs
      '(("pdf" ."okular")
        ("djvu" . "okular")
        ("mp3" . "xmms")))

(defun my-run-async-command (command file)
  "Run a command COMMAND on the file asynchronously.
   No buffers are created"
  (interactive
   (let ((file (car (dired-get-marked-files t current-prefix-arg))))
     (list
      ;last element of alist-programs, contains COMMAND
      (cdr
       (assoc
        (file-name-extension file)
        alist-programs))
      file)))
  ;begin of function body
  (if command ;command if not nil?
      (start-process "command" nil command file)
    )
)

;attach function to <f2> key
(add-hook 'dired-mode-hook
      (lambda ()
    (define-key dired-mode-map (kbd "<f6>") 'my-run-async-command)))
#+end_src


 Create an empty file
 http://stackoverflow.com/questions/2592095
 #+begin_src emacs-lisp
 (eval-after-load 'dired
   '(progn
      (define-key dired-mode-map (kbd "_") 'my-dired-create-file)
      (defun my-dired-create-file (file)
	"Create a file called FILE.
 If FILE already exists, signal an error."
	(interactive
         (list (read-file-name "Create file: " (dired-current-directory))))
	(let* ((expanded (expand-file-name file))
               (try expanded)
               (dir (directory-file-name (file-name-directory expanded)))
               new)
          (if (file-exists-p expanded)
              (error "Cannot create file %s: file exists" expanded))
          ;; Find the topmost nonexistent parent dir (variable `new')
          (while (and try (not (file-exists-p try)) (not (equal new try)))
            (setq new try
                  try (directory-file-name (file-name-directory try))))
          (when (not (file-exists-p dir))
            (make-directory dir t))
          (write-region "" nil expanded t)
          (when new
            (dired-add-file new)
            (dired-move-to-filename))))))
 #+end_src

Act always recursively
#+begin_src emacs-lisp
  (setq dired-recursive-deletes 'always)
  (setq dired-recursive-copies 'always)
#+end_src

 Key bindings.
 #+begin_src emacs-lisp
   (define-key dired-mode-map (kbd "K") 'dired-k)
   (global-set-key "\C-p" 'dired-jump)
 #+end_src
 Helm uses =C-l= to jump to parent dir, consider using this globaly.

** Openwith

By now, load associations, but disabled by default.
#+begin_src emacs-lisp
  (when (require 'openwith nil 'noerror)
    (setq openwith-associations
          (list
           (list (openwith-make-extension-regexp
                  '("mpg" "mpeg" "mp3" "mp4"
                    "avi" "wmv" "wav" "mov" "flv"
                    "ogm" "ogg" "mkv"))
                 "vlc"
                 '(file))
           (list (openwith-make-extension-regexp
                  '("xbm" "pbm" "pgm" "ppm" "pnm"
                    "png" "gif" "bmp" "tif" "jpeg" "jpg"))
                 "eog"
                 '(file))
           (list (openwith-make-extension-regexp
                  '("odt" "ods" "odg" "odp"
                    "doc" "xls" "ppt"
                    "docx" "xlsx" "pptx"))
                 "libreoffice"
                 '(file))
           '("\\.lyx" "lyx" (file))
           '("\\.chm" "kchmviewer" (file))
           (list (openwith-make-extension-regexp
                  '("pdf" "ps" "ps.gz" "dvi"))
                 "okular"
                 '(file))
           ))
    (openwith-mode -1)) ;; Disabled by default
#+end_src
Issues:
- TODO: fix file paths when in dired over tramp.
- TODO: fix conflict of file copy/rename when using helm.
- TODO: disable for =org mode= files, it conflics particularly with =epresent=.

** Web browsing with eww

Brake lines but not words.
#+begin_src emacs-lisp
   (add-hook 'eww-mode-hook 'visual-line-mode)
#+end_src

** Searching

TODO: configure and use this
#+begin_src emacs-lisp
 ;; (require 'engine-mode)
 ;; (defengine mail "https://mail.google.com/mail/u/0/#search/%s" :keybinding "m")
 ;; (defengine google "http://google.com/search?q=%s" :keybinding "g")
 ;; (defengine emacswiki "http://google.com/search?q=site:emacswiki.org+%s" :keybinding "e")
#+end_src

** Windows

 Other window
 #+begin_src emacs-lisp
 (global-set-key (kbd "<C-iso-lefttab>") 'other-window)
 #+end_src

 Windows history
 #+begin_src emacs-lisp
 (winner-mode 1)
 #+end_src

** Tramp

Default method, ssh is faster than scp, [[https://www.emacswiki.org/emacs/TrampMode][ref]].
#+begin_src emacs-lisp
  (setq tramp-default-method "ssh")
#+end_src

** Hide/Show

#+begin_src emacs-lisp
  (require 'hideshow)
;;  (require 'hideshow-org)
#+end_src

Better key bindings for the Latin American keyboard. Thanks to [[https://emacs.stackexchange.com/questions/33684][this post]].
#+begin_src emacs-lisp
  (define-key hs-minor-mode-map (kbd "C-c C-h") (lookup-key hs-minor-mode-map (kbd "C-c @")))
  (define-key hs-minor-mode-map (kbd "C-c @") nil)
#+end_src

Hooks
#+begin_src emacs-lisp
  (add-hook 'c-mode-common-hook   'hs-minor-mode)
  (add-hook 'emacs-lisp-mode-hook 'hs-minor-mode)
  (add-hook 'java-mode-hook       'hs-minor-mode)
  (add-hook 'sh-mode-hook         'hs-minor-mode)
  (add-hook 'python-mode-hook     'hs-minor-mode)
#+end_src

* Appeareance

** Transparency

 #+begin_src emacs-lisp
   (set-frame-parameter (selected-frame) 'alpha '(85 85))
   (add-to-list 'default-frame-alist '(alpha . (85 . 85)))
   ;; http://pages.sachachua.com/.emacs.d/Sacha.html
   (defun sanityinc/adjust-opacity (frame incr)
     (let* ((oldalpha (or (frame-parameter frame 'alpha) 100))
            (newalpha (+ incr oldalpha)))
       (when (and (<= frame-alpha-lower-limit newalpha) (>= 100 newalpha))
         (modify-frame-parameters frame (list (cons 'alpha newalpha))))))
   (global-set-key (kbd "M-C-8") (lambda () (interactive) (sanityinc/adjust-opacity nil -2)))
   (global-set-key (kbd "M-C-9") (lambda () (interactive) (sanityinc/adjust-opacity nil 2)))
   (global-set-key (kbd "M-C-0") (lambda () (interactive) (modify-frame-parameters nil `((alpha . 100)))))
 #+end_src
 TODO: fix a little conflict here, default values brake the interactive adjustment.

** Smart mode line

 #+begin_src emacs-lisp
   (require 'smart-mode-line)
   (setq sml/no-confirm-load-theme t)
   (setq sml/theme 'powerline)
   (sml/setup)
 #+end_src

** Global scale text

 http://www.emacswiki.org/emacs/GlobalTextScaleMode
 #+begin_src emacs-lisp
 (define-globalized-minor-mode
   global-text-scale-mode
   text-scale-mode
   (lambda () (text-scale-mode 1)))
 (defun global-text-scale-adjust (inc) (interactive)
	(text-scale-set 1)
	(kill-local-variable 'text-scale-mode-amount)
	(setq-default text-scale-mode-amount (+ text-scale-mode-amount inc))
	(global-text-scale-mode 1)
	)
 (global-set-key (kbd "M-0")
		 '(lambda () (interactive)
		    (global-text-scale-adjust (- text-scale-mode-amount))
		    (global-text-scale-mode -1)))
 (global-set-key (kbd "M-+")
		 '(lambda () (interactive) (global-text-scale-adjust 1)))
 (global-set-key (kbd "M--")
		 '(lambda () (interactive) (global-text-scale-adjust -1)))
 #+end_src

** Themes

*** Color themes

 Load
 #+begin_src emacs-lisp
   (require 'color-theme)
   (color-theme-initialize)
 #+end_src

*** Custom themes

 Display theme in mode line, disabled
 #+begin_src emacs-lisp
   ;; (require 'display-theme)
   ;; (global-display-theme-mode)
 #+end_src

 http://www.tech-thoughts-blog.com/2013/08/

 issue: loading any theme without asking is not safe.

 issue: if you load a theme, it goes to the desktop record, you close emacs, and open a new frame. Then, the new frame hasn't the theme.

 Load random theme
 #+begin_src emacs-lisp
   (defun load-random-theme ()
     "Load any random theme from the available ones."
     (interactive)
     ;; disable any previously set theme
     (if (boundp 'theme-of-the-day)
         (progn
           (disable-theme theme-of-the-day)
           (makunbound 'theme-of-the-day)))
     (defvar themes-list (custom-available-themes))
     (defvar theme-of-the-day (nth (random (length themes-list))
                                   themes-list))
     (load-theme (princ theme-of-the-day) t)
     (my-set-default-font)
     ;; this function messes with the coding system, I don't know why,
     ;; this is a patch
     (prefer-coding-system 'utf-8)
     )
   ;;(global-set-key (kbd "S-<f4>") 'load-random-theme)
 #+end_src

 issue: =my-set-default-font= works only in the current frame.

*** Default theme

#+begin_src emacs-lisp
  (when (display-graphic-p)
    (load-theme 'monokai t))
#+end_src

** Font

 TODO: clean this
 #+begin_src emacs-lisp
   (set-default-font "-unknown-DejaVu Sans Mono-normal-normal-normal-*-18-*-*-*-m-0-iso10646-1")
 #+end_src

 A function and a key binding in case a custom theme doesn't behaves
 #+begin_src emacs-lisp
   (defun my-set-default-font ()
     (interactive)
     (set-default-font "-unknown-DejaVu Sans Mono-normal-normal-normal-*-18-*-*-*-m-0-iso10646-1"))
   (global-set-key (kbd "S-<f2>") 'my-set-default-font)
 #+end_src

** White space

 Remove white space for some modes
 #+begin_src emacs-lisp
 (add-hook 'c-mode-hook
	   (lambda () (add-to-list 'write-file-functions 'delete-trailing-whitespace)))
 (add-hook 'cuda-mode-hook
	   (lambda () (add-to-list 'write-file-functions 'delete-trailing-whitespace)))
 (add-hook 'sh-mode-hook
	   (lambda () (add-to-list 'write-file-functions 'delete-trailing-whitespace)))
 (add-hook 'emacs-lisp-mode-hook
	   (lambda () (add-to-list 'write-file-functions 'delete-trailing-whitespace)))
 #+end_src

*** Whitespace mode

 http://ergoemacs.org/emacs/whitespace-mode.html
 #+begin_src emacs-lisp
   (progn
     ;;  Make whitespace-mode with very basic background coloring for whitespaces.
     (setq whitespace-style (quote ( spaces tabs newline space-mark tab-mark newline-mark )))
     ;; Make whitespace-mode and whitespace-newline-mode use “¶” for end of line char and “▷” for tab.
     (setq whitespace-display-mappings
           ;; all numbers are unicode codepoint in decimal. e.g. (insert-char 182 1)
           '(
             (space-mark 32 [183] [46]) ; SPACE 32 「 」, 183 MIDDLE DOT 「·」, 46 FULL STOP 「.」
             (newline-mark 10 [182 10]) ; LINE FEED,
             (tab-mark 9 [9655 9] [92 9]) ; tab
             )))
 #+end_src

** Manual highlithing

 #+begin_src emacs-lisp
 (require 'highlight)
 #+end_src

** Window handling

 Resize
 #+begin_src emacs-lisp
 (global-set-key (kbd "C-<left>") 'shrink-window-horizontally)
 (global-set-key (kbd "C-<right>") 'enlarge-window-horizontally)
 (global-set-key (kbd "C-<down>") 'shrink-window)
 (global-set-key (kbd "C-<up>") 'enlarge-window)
 #+end_src

***  toggle windows split
 http://www.emacswiki.org/emacs/ToggleWindowSplit
 #+begin_src emacs-lisp
   (defun toggle-window-split ()
     (interactive)
     (if (= (count-windows) 2)
         (let* ((this-win-buffer (window-buffer))
		(next-win-buffer (window-buffer (next-window)))
		(this-win-edges (window-edges (selected-window)))
		(next-win-edges (window-edges (next-window)))
		(this-win-2nd (not (and (<= (car this-win-edges)
                                            (car next-win-edges))
					(<= (cadr this-win-edges)
                                            (cadr next-win-edges)))))
		(splitter
                 (if (= (car this-win-edges)
			(car (window-edges (next-window))))
                     'split-window-horizontally
                   'split-window-vertically)))
           (delete-other-windows)
           (let ((first-win (selected-window)))
             (funcall splitter)
             (if this-win-2nd (other-window 1))
             (set-window-buffer (selected-window) this-win-buffer)
             (set-window-buffer (next-window) next-win-buffer)
             (select-window first-win)
             (if this-win-2nd (other-window 1))))))
   ;;
   (define-key ctl-x-4-map "t" 'toggle-window-split)
 #+end_src

** fullframe

#+begin_src emacs-lisp
  (require 'fullframe)
  (fullframe magit-status magit-mode-quit-window nil)
#+end_src

** Smoth scrolling

#+begin_src emacs-lisp
  (require 'smooth-scrolling)
  (smooth-scrolling-mode 1)
#+end_src

* Writing and coding

Do not use tabs for indentation
#+begin_src emacs-lisp
  (setq-default indent-tabs-mode nil)
#+end_src

Zap up to char, thanks to [[https://www.emacswiki.org/emacs/ZapUpToChar][emacs wiki]]
#+begin_src emacs-lisp
  (autoload 'zap-up-to-char "misc"
    "Kill up to, but not including ARGth occurrence of CHAR.

  \(fn arg char)"
    'interactive)

  (global-set-key "\M-z" 'zap-up-to-char)
#+end_src

** LaTeX

 Load
 #+begin_src emacs-lisp
   (load "auctex.el" nil t t)
 #+end_src

 Parsing, [[https://www.gnu.org/software/auctex/manual/auctex/Parsing-Files.html][ref]]. This generates the =auto= directory.
 #+begin_src emacs-lisp
   (setq TeX-parse-self t) ; Enable parse .tex files on load.
   (setq TeX-auto-save t) ; Enable parse .tex files on save (auto dir).
 #+end_src

 Reftex
 #+begin_src emacs-lisp
   (add-hook 'LaTeX-mode-hook 'turn-on-reftex)
 #+end_src

 Math mode
 #+begin_src emacs-lisp
   (add-hook 'LaTeX-mode-hook 'LaTeX-math-mode)
 #+end_src

 Fold mode
 #+begin_src emacs-lisp
   (add-hook 'LaTeX-mode-hook
             (lambda () (TeX-fold-mode 1)))
 #+end_src

 Flyspell
 #+begin_src emacs-lisp
 (add-hook 'LaTeX-mode-hook 'flyspell-mode)
 #+end_src

 Visual line mode
 #+begin_src emacs-lisp
 (add-hook 'LaTeX-mode-hook 'visual-line-mode)
 #+end_src

 Omit custom latex commands and environments in ispell
 http://tex.stackexchange.com/questions/117204
 #+begin_src emacs-lisp
 (setq ispell-tex-skip-alists
       (list
	(append
     (car ispell-tex-skip-alists)
     '(
       ("\\\\citep"       ispell-tex-arg-end)
       ("\\\\eqref"       ispell-tex-arg-end)
       ;; add as many lines like the previous two as you need before the "))"
       ))
	(append
     (cadr ispell-tex-skip-alists)
     '(
       ("align\\*?" . "\\\\end[      \n]*{[  \n]*align\\*?[  \n]*}")
       ;; add as many lines like the previous two as you need before the "))))"
       ))))
 #+end_src

 Integration with pdf-tools
 #+begin_src emacs-lisp
 (setq TeX-source-correlate-method 'SyncTeX)
 #+end_src

*** Bibtex

Silly macro to copy the key in a bibtex file
#+begin_src emacs-lisp
  (fset 'my-copy-bibtex-key
     [?\C-r ?@ return ?\C-s ?\{ return ?\C-\M-  ?\M-w])
#+end_src
TODO: write a real elisp function to do this. Something like:
#+begin_src emacs-lisp :exports none :tangle no
  (defun my-bibtex-cmd
      (save-excursion
        (progn
          (clean )
          (format )
          (search-back @ )
          (forward-word )
          (setmark)
          (forward-word )
          (copy-region-kill))))
  (set-local-keybinding )
#+end_src

** org cdlatex

Load
#+begin_src emacs-lisp
  (require 'cdlatex)
  (add-hook 'cdlatex-mode-hook 'reftex-mode)
#+end_src

More convenient key binding, for latin american (spanish) keyboard.
#+begin_src emacs-lisp
  (add-hook 'cdlatex-mode-hook
	    (lambda ()
	    (local-set-key "°" 'cdlatex-math-symbol) ) )
#+end_src

** C++ mode

 #+begin_src emacs-lisp
   (c-set-offset 'case-label '+)
   (add-to-list 'auto-mode-alist '("\\.inc\\'" . c++-mode))
 #+end_src

** CUDA mode

 #+begin_src emacs-lisp
   (add-to-list 'auto-mode-alist '("\\.cu\\'" . c++-mode))
   (add-to-list 'auto-mode-alist '("\\.cuh\\'" . c++-mode))
   (add-to-list 'auto-mode-alist '("\\.cuinc\\'" . c++-mode))
   ;; There is a problem with the cuda-mode
 #+end_src
 - a way to add multiple elements to the list would be more elegant.

** org mode

Load
#+begin_src emacs-lisp
  (require 'org)
  (setq org-log-done t)
#+end_src

 Key bindings, TODO: clean this
 #+begin_src emacs-lisp
   (global-set-key "\C-cl" 'org-store-link)
   (global-set-key "\C-cc" 'org-capture)
   (global-set-key "\C-ca" 'org-agenda)
   (global-set-key "\C-cb" 'org-iswitchb)
   (defun my-bindkey-move-one-line ()
     "Override local key with the global key."
     (interactive)
     ;; (local-set-key (kbd "M-<down>") 'custom-down-one-line)
     ;; (local-set-key (kbd "M-<up>") 'custom-up-one-line)
     (local-set-key (kbd "<C-tab>") 'other-window))
   (add-hook 'org-mode-hook 'my-bindkey-move-one-line)
 #+end_src

 File extension association
 #+begin_src emacs-lisp
 (add-to-list 'auto-mode-alist '("\\.org\\'" . org-mode))
 #+end_src
 This has trouble when file variables declare minor modes, like reftex.

 Personal org files, and capture
 #+begin_src emacs-lisp
 ;; notes and agenda directory
 (setq org-directory "~/personal/org/")
 ;; http://superuser.com/questions/635279
 (setq org-capture-templates
       (quote (("t" "todo" entry (file (concat org-directory "gtd.org"))
		"* TODO %?\n%U\n%a\n" :clock-in t :clock-resume t)
	       ("b" "bibcard" entry (file (concat org-directory "bibcards.org"))
		"* \n%U\n%a\n#+BEGIN_QUOTE\n%?\n#+END_QUOTE")
               ("n" "note" entry (file (concat org-directory "gtd.org"))
		"* %? :NOTE:\n%U\n%a\n" :clock-in t :clock-resume t)
               ("j" "Journal" entry (file+datetree (concat org-directory "diary.org"))
		"* %?\n%U\n" :clock-in t :clock-resume t)
               )))
 #+end_src

Capture for projectile projects
#+begin_src emacs-lisp
  (require 'org-projectile)
  ;; (setq org-projectile:projects-file
  ;;       "~/personal/org/projectile.org")
  ;; (add-to-list 'org-capture-templates (org-projectile:project-todo-entry))
  ;; (setq org-agenda-files (append org-agenda-files (org-projectile:todo-files)))
  ;; (global-set-key (kbd "C-c n p") 'org-projectile:project-todo-completing-read)
#+end_src


 Create IDs, [[http://stackoverflow.com/questions/13340616/][ref]], the idea here is to be able to describe code issues in org files, and reference the issue description in the git commit messages.
 #+begin_src emacs-lisp
   (defvar my-org-add-ids t)
   (defun my/org-add-ids-to-headlines-in-file ()
     "Add ID properties to all headlines in the current file which
   do not already have one."
     (interactive)
     (if my-org-add-ids
        (org-map-entries 'org-id-get-create)))
   ;; (add-hook 'org-mode-hook
   ;;           (add-hook 'before-save-hook 'my/org-add-ids-to-headlines-in-file nil 'local))
 #+end_src
 - I'm still testing the conditional behaviour of this.
 - I would prefer to add the hook only when the file variable is =t=, but that was not working. I don't know if the hooks or the local variables are read first.

 Use IDs for links
 #+begin_src emacs-lisp
   (setq org-id-link-to-org-use-id t)
 #+end_src

 Template to insert lisp code
 #+begin_src emacs-lisp
   (add-to-list
    'org-structure-template-alist
    '("E" "#+begin_src emacs-lisp\n?\n#+end_src" "<src lang=\"emacs-lisp\">\n?\n</src>"))
   (add-to-list
    'org-structure-template-alist
    '("j" "#+begin_src java\n?\n#+end_src" "<src lang=\"java\">\n?\n</src>"))
 #+end_src

 Template to insert =artist-mode= pictures
 #+begin_src emacs-lisp
   (add-to-list
    'org-structure-template-alist
    '("z" "#+begin_src artist\n?\n#+end_src" "<div class=\"ascii-art\">\n?\n</div>"))
 #+end_src
TODO: insert buttons in =artist-mode=. To select different tools of the =artist-mode= by clicking on them.

 Visual line mode
 #+begin_src emacs-lisp
 (with-eval-after-load 'org
   (add-hook 'org-mode-hook 'visual-line-mode))
 #+end_src

Export with bibtex
#+begin_src emacs-lisp
 (require 'ox-bibtex)
#+end_src

Skip confirmation when evaluating code blocks during a presentation, and some configuration on scaling to fit my screen.
#+begin_src emacs-lisp
  (require 'epresent)
  (require 'ob-sh)
  (add-hook 'epresent-start-presentation-hook
	    (lambda () (progn (setq org-confirm-babel-evaluate nil)
			      (setq org-format-latex-options '(:foreground default :background default :scale 4.0 :html-foreground "Black" :html-background "Transparent" :html-scale 1.0 :matchers ("begin" "$1" "$" "$$" "\\(" "\\["))))))
#+end_src

Hide markup elements, [[http://stackoverflow.com/questions/10969617/hiding-markup-elements-in-org-mode][ref]].
#+begin_src emacs-lisp
  (setq org-hide-emphasis-markers t)
#+end_src

Highlight code, [[http://stackoverflow.com/questions/17239273][ref]].
#+begin_src emacs-lisp
  (setq org-src-fontify-natively t)
  (setq org-highlight-latex-and-related '(latex script entities))
#+end_src

Bibtex was not been called when doing a latex export, to solve this
#+begin_src emacs-lisp
  ;; (setq org-latex-pdf-process
  ;;    "latexmk -pdflatex='pdflatex -shell-escape -interaction nonstopmode' -pdf -g -f  %f")
#+end_src
taken from [[http://tex.stackexchange.com/questions/114864][here]]. Not working, IDNW.

Template to insert latex equation, directly
 #+begin_src emacs-lisp
   (add-to-list
    'org-structure-template-alist
    '("la" "\\begin{align*}\n  ?\n\\end{align*}"))
 #+end_src

Use quotes when exporting to latex a document in spanish
#+begin_src emacs-lisp
  (add-to-list 'org-export-smart-quotes-alist
               '("es"
                 (primary-opening :utf-8 "“" :html "&ldquo;" :latex "``" :texinfo "``")
                 (primary-closing :utf-8 "”" :html "&rdquo;" :latex "''" :texinfo "''")
                 (secondary-opening :utf-8 "‘" :html "&lsquo;" :latex "`" :texinfo "`")
                 (secondary-closing :utf-8 "’" :html "&rsquo;" :latex "'" :texinfo "'")
                 (apostrophe :utf-8 "’" :html "&rsquo;")))
#+end_src

Template to insert my common preferences in the header
#+begin_src emacs-lisp
     (add-to-list
      'org-structure-template-alist
      '("hh" "#+TITLE:\n#+AUTHOR:\n#+OPTIONS: toc:nil ':t num:nil\n#+OPTIONS: html-postamble:nil\n#+LANGAGE: es\n#+LaTeX_HEADER: \\usepackage[spanish]{babel}\n#+LaTeX_HEADER: \\selectlanguage{spanish}\n#+LaTeX_HEADER: \\usepackage{physics}\n#+LaTeX_HEADER: \\hypersetup{hidelinks=true}\n#+LaTeX_HEADER: \\addtolength{\\parskip}{\\baselineskip}\n#+LaTeX_HEADER: \\usepackage{enumitem}\n#+LaTeX_HEADER: \\setlist{noitemsep}\n#+LaTeX_HEADER: \\setlist[itemize]{parsep=0mm}\n#+LaTeX_HEADER: \\setlist[itemize]{partopsep=0mm}\n#+LaTeX_HEADER: \\setlist[itemize]{itemsep=0mm}\n#+LaTeX_HEADER: \\setlist[itemize]{topsep=0mm}\n#+LaTeX_HEADER: \\usepackage{geometry}\n#+LaTeX_HEADER: \\geometry{letterpaper,total={183mm,247mm},left=10mm,top=10mm}\n#+LaTeX: \\decimalpoint\n\n"))
#+end_src

** Text mode

   Visual line mode
   #+begin_src emacs-lisp
     (add-hook 'text-mode-hook 'visual-line-mode)
   #+end_src

** Markdown mode

#+begin_src emacs-lisp
(require 'markdown-mode )
#+end_src

 Extension association
 #+begin_src emacs-lisp
   (add-to-list 'auto-mode-alist '("\\.md\\'" . markdown-mode))
 #+end_src

 Visual line mode
 #+begin_src emacs-lisp
   (add-hook 'markdown-mode-hook 'visual-line-mode)
 #+end_src

Insert inline math for gitlab flavored markdown
#+begin_src emacs-lisp
  (defun markdown-insert-gitlab-inline-math ()
    (interactive)
    (if (markdown-use-region-p)
        ;; Active region
        (let ((bounds (markdown-unwrap-things-in-region
                       (region-beginning) (region-end)
                       markdown-regex-code 1 3)))
          (markdown-wrap-or-insert "$`" "`$" nil (car bounds) (cdr bounds)))
      ;; Code markup removal, code markup for word, or empty markup insertion
      (if (markdown-inline-code-at-point)
          (markdown-unwrap-thing-at-point nil 0 2)
        (markdown-wrap-or-insert "$`" "`$" 'word nil nil))))

  (define-key markdown-mode-style-map (kbd "m") 'markdown-insert-gitlab-inline-math)
#+end_src

** Octave

 File extension association
 #+begin_src emacs-lisp
   (add-to-list 'auto-mode-alist '("\\.m\\'" . octave-mode))

 #+end_src

Trying to mimic matlab comment style in octave mode
#+begin_src emacs-lisp
  ;;(setq octave-comment-start "%")
  (setq octave-mode-hook
        (lambda () (progn (setq octave-comment-char ?%)
                          (setq comment-start "% ")
                          (setq comment-add 0)
                          (setq comment-column 0))))

  ;; This doesn't work
  ;; (add-hook 'octave-mode-hook
  ;;           (lambda ()
  ;;             (define-key octave-mode-map (kbd "M-;") 'matlab-comment)))
#+end_src
** Maxima

From [[https://www.emacswiki.org/emacs/MaximaMode][here]], omitting the path loading. Beacuse it's different, and
already loaded in my computer.

#+begin_src emacs-lisp
 (autoload 'maxima-mode "maxima" "Maxima mode" t)
 (autoload 'imaxima "imaxima" "Frontend for maxima with Image support" t)
 (autoload 'maxima "maxima" "Maxima interaction" t)
 (autoload 'imath-mode "imath" "Imath mode for math formula input" t)
 (setq imaxima-use-maxima-mode-flag t)
 (add-to-list 'auto-mode-alist '("\\.ma[cx]" . maxima-mode))
#+end_src

When working on a maxima file-buffer, to use =imaxima= as the inferior
mode, open the inferior buffer =M-x imaxima=, before sending the buffer
to maxima.
** Julia

Thanks to [[https://tpapp.github.io/post/emacs-julia-customizations/][this post]]
#+begin_src emacs-lisp
  (defun customize-julia-mode ()
    "Customize julia-mode."
    (interactive)
    (local-set-key (kbd "M-q") 'julia-fill-string)
    )

  (add-hook 'julia-mode-hook 'customize-julia-mode)
#+end_src

Fill docstrings
#+begin_src emacs-lisp
    (defun julia-fill-string ()
      "Fill a docstring, preserving newlines before and after
  triple quotation marks."
      (interactive)
      (if (and transient-mark-mode mark-active)
          (fill-region (region-beginning) (region-end) nil t)
        (cl-flet
            ((fill-if-string
              ()
              (when
                  (or (looking-at (rx "\"\"\""
                                      (group
                                       (*? (or (not (any "\\"))
                                               (seq "\\" anything))))
                                      "\"\"\""))
                      (looking-at (rx "\""
                                      (group
                                       (*? (or (not (any "\\"))
                                               (seq "\\" anything))))
                                      "\"")))
                (let ((start (match-beginning 1))
                      (end (match-end 1)))
                  ;; (ess-blink-region start end)
                  (fill-region start end nil nil nil)))))
          (save-excursion
            (let ((s (syntax-ppss)))
              (when (fourth s) (goto-char (ninth s))))
            (fill-if-string)))))
#+end_src

* Drawing

** Artist mode

   File extension association
   #+begin_src emacs-lisp
     (add-to-list 'auto-mode-alist '("\\.art\\'" . artist-mode))
   #+end_src
   This collides with an AOL image format, but I think it's not a big problem.

* Firefox integration

#+begin_src emacs-lisp
  ;; (when (require 'edit-server nil t)
  ;;   (setq edit-server-new-frame nil)
  ;;   (edit-server-start))

  ;; (setq edit-server-url-major-mode-alist
  ;;       '(("sharelatex\\.com" . LaTeX-mode)))
#+end_src
